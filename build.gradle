apply plugin: 'com.github.kt3k.coveralls'
apply plugin: 'jacoco'
apply plugin: 'java'
apply plugin: 'org.asciidoctor.jvm.convert'
apply plugin: 'org.asciidoctor.jvm.pdf'

buildscript {
  apply from: "./gradle/dependencies.gradle"

  repositories {
    maven { url "https://repo.spring.io/plugins-release" }
    maven { url "https://repo.spring.io/libs-release" }
    maven { url "https://repo.spring.io/libs-milestone" }
    maven { url "https://plugins.gradle.org/m2/" }
	gradlePluginPortal()
    mavenCentral()
    jcenter()
  }

  dependencies {
    classpath gradlePlugins.values()
  }
}

allprojects {
	repositories {
		mavenCentral()
		maven { url("https://plugins.gradle.org/m2/") }
		maven { url 'https://repo.spring.io/snapshot' }
		maven { url 'https://repo.spring.io/milestone' }
		maven {
			url uri("https://maven.pkg.github.com/tscz/spring-boot-platform")
			credentials {
				username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
				password = project.findProperty("gpr.key") ?: System.getenv("TOKEN")
			}
		 }
	}
}

subprojects {
	group = 'com.github.tscz.jalp'
	version = '0.0.1-SNAPSHOT'
	
	apply plugin: "java"
	apply plugin: "jacoco"
	
	jacoco {
        toolVersion = pluginVersions.jacoco
    }
    
	jacocoTestReport {
        reports {
            xml.enabled = true // coveralls plugin depends on xml format report
            html.enabled = true
        }
    }
    
    test {
		useJUnitPlatform()
	}

	task allDeps(type: DependencyReportTask) {}
	
	tasks.withType(JavaCompile) {
    	options.compilerArgs += "--enable-preview"
	}
	tasks.withType(Test) {
	    jvmArgs += "--enable-preview"
	}
	tasks.withType(JavaExec) {
	    jvmArgs += "--enable-preview"
	}

	javadoc.options {
	    addBooleanOption('-enable-preview', true)
	    addStringOption('-release', '15')
	}
}

jacocoTestReport {
	reports {
		xml.enabled = true // coveralls plugin depends on xml format report
		html.enabled = true
	}
}

test {
	useJUnitPlatform()
}

// coveralls setup
task jacocoRootReport(type: JacocoReport) {
    description = 'Generates an aggregate report from all subprojects'
    dependsOn = subprojects.test
    sourceDirectories.from files(subprojects.sourceSets.main.allSource.srcDirs)
    classDirectories.from  files(subprojects.sourceSets.main.output)
    executionData.from files(subprojects.jacocoTestReport.executionData)
    reports {
        html.enabled = true
        xml.enabled = true
    }
    
    jacoco {
        toolVersion = pluginVersions.jacoco
    }
}

coveralls {
	jacocoReportPath = "${buildDir}/reports/jacoco/jacocoRootReport/jacocoRootReport.xml"
  	saveAsFile = true
	sendToCoveralls = false
    sourceDirs = subprojects.sourceSets.main.allSource.srcDirs.flatten()
}


asciidoctor {
	outputOptions {
	        backends = ['html5', 'pdf']
	    }

	options doctype: 'book'
	attributes \
		'toc' : 'left' 
}